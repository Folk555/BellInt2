plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
}

group 'ru.turulin'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

dependencies {

    // rest-assured
    testImplementation 'io.rest-assured:rest-assured:5.3.1'
    testImplementation 'io.rest-assured:rest-assured-all:5.3.1'

    // junit 5
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    // для запуска cucumber тестов через junit runner
    testImplementation 'org.junit.platform:junit-platform-suite-api:1.10.0'
    testImplementation 'org.junit.platform:junit-platform-runner:1.10.0'

    //cucumber
    testImplementation 'io.cucumber:cucumber-java:7.13.0'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.13.0'

    //Allure - Автоматизированная постройка отчета
    implementation 'io.qameta.allure:allure-cucumber7-jvm:2.23.0'


    // чтобы логи restAssured добавлялись к allure отчету
    implementation 'io.qameta.allure:allure-rest-assured:2.23.0'

}

test {
    useJUnitPlatform()
    //добавляем логирование имен фич в тесты для большей читаемости
    systemProperty("cucumber.junit-platform.naming-strategy", "long")
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty',
                    '--plugin', 'io.qameta.allure.cucumber7jvm.AllureCucumber7Jvm', '--glue', 'steps', 'src/test/resources/features']
            jvmArgs "--add-opens=java.base/java.lang=ALL-UNNAMED"
        }
    }
}
cucumber.finalizedBy 'allureReport'

tasks { compileTestJava.options.encoding = 'UTF-8' }